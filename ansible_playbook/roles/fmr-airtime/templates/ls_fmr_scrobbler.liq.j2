# ATTENTION : fichier géré par Ansible ! Les modifications seront perdues.

# Based on Voisses Tech's works:
# https://forum.sourcefabric.org/discussion/comment/30873#Comment_30873

# Usage:
# - Modify Airtime configuration file:
# /usr/lib/airtime/pypo/bin/liquidsoap_scripts/ls_script.liq
#
# - If overriding the "default" playlist then you must stop to rewrite metadata
# to "Offline" string. Modify around line 160 to keep the "title" metadata:
##default = rewrite_metadata([("title", !ref_off_air_meta)], default)
#default = rewrite_metadata([("comment", !ref_off_air_meta)], default)
#
# - Then just include this file and call the scrobble() method on the stream
# variable. Around line 320, add these lines:
#%include "/path/to/liquidsoap_scrobbler.liq"
#s = on_track(scrobble, s)

def scrobble(m) =

    ######################################################################
    # NOTE: Change these variables to your own values
    ######################################################################

    partnerId  = "{{ scrobbler_partner_id }}"
    partnerKey = "{{ scrobbler_partner_key }}"
    id         = "{{ scrobbler_id }}"
    api_server = "{{ scrobbler_url }}"

    airtime_live_info = "http://localhost/api/live-info/type/endofday"
    airtime_week_info = "http://localhost/api/week-info"

    ######################################################################
    # Normally there is nothing to change below
    ######################################################################

    #station    = url.encode(!station_name)
    # NOTE: "show" value is not empty between two shows, so the fallback
    # "default" playlist tracks appears to belong to previous show :-(
    #show       = url.encode(!show_name)
    artist     = url.encode(m["artist"])
    album      = url.encode(m["album"])
    title      = url.encode(m["title"])
    commercial = url.encode(m["commercial"])

    # Retrieve duration in seconds: 192.83
    file_path = m["filename"]
    duration = string_of(file.duration(file_path))

    # Get Airtime's {live,week}-info JSON to pass them to the scrobbler
    live_info = url.encode(snd(http.get(airtime_live_info)))
    week_info = url.encode(snd(http.get(airtime_week_info)))

    post_data =
        "partnerId="    ^ partnerId ^
        "&partnerKey="  ^ partnerKey ^
        "&id="          ^ id ^
        #"&station="     ^ station ^
        #"&show="        ^ show ^
        "&artist="      ^ artist ^
        "&album="       ^ album ^
        "&title="       ^ title ^
        "&duration="    ^ duration ^
        #"&commercial="  ^ commercial ^
        "&live_info="   ^ live_info ^
        "&week_info="   ^ week_info

    # Don't submit commercials (Jingles, etc.)
    if commercial != "true" then
        # Printout the URL in the log
        log("Scrobbler Server (POST data hidden): " ^ api_server)
        #log("Scrobbler POST Data: " ^ post_data)

        # Actually do the HTTP request
        ignore(http.post(
                    api_server,
                    data=post_data,
                    headers=[("Content-Type","application/x-www-form-urlencoded")],
                ))
    end

end

# vim: ft=ruby
